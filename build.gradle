plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'application'
    id "com.github.ben-manes.versions" version "0.21.0"
}

repositories {
    mavenCentral()
}

def jvmVersion = '1.8'
def jdkVersion = System.getProperty('java.version')
def kotlinVersion = '1.3.21'
def loggingVersion = '1.6.25'
def sparkVersion = '2.8.0'
def jacksonVersion = '2.9.8'
def slf4jVersion = '1.7.21'
def junitVersion = '5.4.0'
def appName = 'starter-snake-kotlin'

// Update this value to change the snake you want to run
def mainName = 'io.battlesnake.starter.Snake'

group = 'io.battlesnake'
version = '0.0.1-SNAPSHOT'
description = appName
sourceCompatibility = jvmVersion

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "io.github.microutils:kotlin-logging:$loggingVersion"
    implementation "com.sparkjava:spark-core:$sparkVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "org.slf4j:slf4j-simple:$slf4jVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

// This determines the main() executed with ./gradlew run
application {
    mainClassName = mainName
}

compileKotlin {
    kotlinOptions {
        jvmTarget = jvmVersion
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = jvmVersion
    }
}

test {
    useJUnitPlatform()
}

// Include build uberjars in heroku deploy
task stage(dependsOn: ['uberjar', 'build', 'clean'])
build.mustRunAfter clean

// This task creates the uberjar that enables the app to run with: java -jar jarName
task uberjar(type: Jar, dependsOn: [':compileJava', ':processResources']) {
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes 'Implementation-Title': appName,
                'Implementation-Version': version,
                'Built-Date': new Date(),
                'Built-JDK': jdkVersion,
                'Main-Class': mainName
    }

    baseName = appName
    version = ''

    with jar
}

startScripts.enabled = false

// This task creates the shell scripts to run the app
task starterSnake(type: CreateStartScripts) {
    mainClassName = mainName
    applicationName = appName
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(starterSnake)
    fileMode = 0755
}
